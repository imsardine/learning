// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'jacoco'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext {
    COVERAGE_INCLUDES = ['com/example/usage/**/*.class']
    COVERAGE_EXCLUDES = ['**/R.class', '**/R$*.class', '**/Manifest.class', '**/Manifest$*.class', '**/BuildConfig.class']

    SUPPORT_LIBRARY_VERSION = '25.1.0'
}

task jacocoTestReport(type: JacocoReport) {
    def appReport = tasks.getByPath('app:jacocoTestReport')
    def javalibReport = tasks.getByPath('javalib:jacocoTestReport')
    def androidlibReport = tasks.getByPath('androidlib:jacocoTestReport')

    dependsOn appReport, javalibReport, androidlibReport

    executionData = files(
            appReport.executionData,
            javalibReport.executionData,
            androidlibReport.executionData
    )

    sourceDirectories = files(
            appReport.sourceDirectories,
            javalibReport.sourceDirectories,
            androidlibReport.sourceDirectories
    )

    classDirectories = files(
            appReport.classDirectories,
            javalibReport.classDirectories,
            androidlibReport.classDirectories
    )
}
